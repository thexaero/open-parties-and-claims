plugins {
    id 'idea'
    id 'fabric-loom'
}

base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
    compileOnly project(":Common")

    modImplementation "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:20.2.6"//source: https://github.com/Fuzss/forgeconfigapiport-fabric

    modCompileOnly "curse.maven:ftbranks-472659:3831005"//home: https://www.curseforge.com/minecraft/mc-mods/ftb-ranks-fabric
    modCompileOnly "curse.maven:architectury-419699:4581904"//home: https://www.curseforge.com/minecraft/mc-mods/architectury-api
    modCompileOnly "curse.maven:ftblibrary-438495:4596510"//home: https://www.curseforge.com/minecraft/mc-mods/ftb-library-fabric

    modCompileOnly "maven.modrinth:resourceful-lib:5gAcCZfI"//source: https://github.com/Team-Resourceful/ResourcefulLib
    modCompileOnly "maven.modrinth:prometheus:C5LLDjN7"//source: https://github.com/terrarium-earth/Prometheus
    modCompileOnly "net.luckperms:api:5.4"//source: https://github.com/LuckPerms/LuckPerms
    modCompileOnly 'me.lucko:fabric-permissions-api:0.2-SNAPSHOT'//source: https://github.com/lucko/fabric-permissions-api

    modCompileOnly "curse.maven:ftbteams-438497:4616151"//home: https://www.curseforge.com/minecraft/mc-mods/ftb-teams-fabric
    modCompileOnly "maven.modrinth:argonauts:0.1.1"//source: https://modrinth.com/mod/argonauts
}

loom {
    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }
        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}


processResources {
    from project(":Common").sourceSets.main.resources
    inputs.property "version", project.version
}


tasks.withType(JavaCompile) {
    source(project(":Common").sourceSets.main.allSource)
}

jar {
    from("LICENSE") {
        rename { "${it}_${mod_name}" }
    }
}

jar.finalizedBy('packageSources')

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}
