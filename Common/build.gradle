plugins {
    id 'java'
    id 'org.spongepowered.gradle.vanilla' version '0.2.1-SNAPSHOT'
    id 'maven-publish'
}

java.withJavadocJar()

archivesBaseName = "${mod_name}-common-${minecraft_version}"

minecraft {
    version(minecraft_version)
    runs {   
        if (project.hasProperty('common_runs_enabled') ? project.findProperty('common_runs_enabled').toBoolean() : true) {
        
            server(project.hasProperty('common_server_run_name') ? project.findProperty('common_server_run_name') : 'vanilla_server') {
                workingDirectory(this.file("run"))
            }
            client(project.hasProperty('common_client_run_name') ? project.findProperty('common_client_run_name') : 'vanilla_client') {
                workingDirectory(this.file("run"))
            }
        }
    }
}

repositories {
    maven {
        url = "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
}

dependencies {
     compileOnly group:'org.spongepowered', name:'mixin', version:'0.8.5'
     implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

     implementation 'com.electronwill.night-config:core:3.6.3'
     implementation 'com.electronwill.night-config:toml:3.6.3'
     compileOnly "net.minecraftforge:forgeconfigapiport-fabric:3.2.4"//source: https://github.com/Fuzss/forgeconfigapiport-fabric

     //same as forge for the deobfuscated class names in the api
     compileOnly "curse.maven:ftbranks-314905:3599720"//home: https://www.curseforge.com/minecraft/mc-mods/ftb-ranks-forge
     compileOnly "curse.maven:architectury-419699:3824307"//home: https://www.curseforge.com/minecraft/mc-mods/architectury-api
     compileOnly "curse.maven:ftblibrary-404465:3822758"//home: https://www.curseforge.com/minecraft/mc-mods/ftb-library-forge

     compileOnly "net.luckperms:api:5.4"//source: https://github.com/LuckPerms/LuckPerms

     compileOnly"curse.maven:ftbteams-404468:4579981"//home: https://www.curseforge.com/minecraft/mc-mods/ftb-teams-forge

}

processResources {

    def buildProps = project.properties.clone()

    filesMatching(['pack.mcmeta']) {

        expand buildProps
    }
}

javadoc {
    include "**/api/**"

    title "${mod_display_name} API"
    destinationDir file("${rootProject.rootDir}/docs/javadoc")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId project.group
            artifactId project.archivesBaseName
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            url "file://" + System.getenv("local_maven")
        }
    }
}